-- Copyright 2016 Carnegie Mellon University. See Notice.txt
-- Distribution Statement A: Approved for Public Release; Distribution is Unlimited.
--  An Autopilot System package...
package Integrator::FGS::AP
public
with Integrator::FGS::Displays::CDU, Integrator::FGS::SCS,  SEI;

  process prAutoPilot
    features
      pfd1In: feature group Integrator::FGS::Displays::CDU::PFD_OutDataSkt;
      pfd1Out: feature group Integrator::FGS::Displays::CDU::PFD_InDataSkt;
      pfd2In: feature group Integrator::FGS::Displays::CDU::PFD_OutDataSkt;
      pfd2Out: feature group Integrator::FGS::Displays::CDU::PFD_InDataSkt;
      servo1: out feature  Integrator::FGS::SCS::SCS_Data;
      servo2: out feature  Integrator::FGS::SCS::SCS_Data;
      otherAP_A: feature group AP_CrossPlg;
      otherAP_B: feature group AP_CrossSkt;
      FGS2: feature group AP_DataSkt;
      FGS1: feature group AP_DataSkt;
    flows
      fAPCLAWS1: flow path FGS1 -> servo1;
      fAPCLAWS2: flow path FGS1 -> servo2;
    properties
      SEI::MIPSBudget => 50.0 MIPS;
      SEI::RAMBudget => 1.6 MByte;
      SEI::ROMBudget => 16.0 MByte;
      SEI::Is_Partition => true;
      SEI::Partition_Latency => 2000 us;
  end prAutoPilot;
  
  process implementation prAutoPilot.impl
    subcomponents
      THR_AP: thread thrAutoPilot.impl;
    connections
      PortGroupConnection1: feature group pfd1In <-> THR_AP.pfd1In;
      PortGroupConnection2: feature group pfd1Out <-> THR_AP.pfd1Out;
      PortGroupConnection3: feature group pfd2In <-> THR_AP.pfd2In;
      PortGroupConnection4: feature group pfd2Out <-> THR_AP.pfd2Out;
      PortGroupConnection5: feature  THR_AP.servo1 -> servo1;
      PortGroupConnection6: feature  THR_AP.servo2 -> servo2;
      PortGroupConnection7: feature group otherAP_A <-> THR_AP.otherAP_A;
      PortGroupConnection8: feature group THR_AP.otherAP_B <-> otherAP_B;
      PortGroupConnection9: feature group FGS2 <-> THR_AP.FGS2;
      PortGroupConnection10: feature group FGS1 <-> THR_AP.FGS1;
    flows
      fAPCLAWS1: flow path FGS1 -> PortGroupConnection10 -> THR_AP.fAPCLAWS1
         -> PortGroupConnection5 -> servo1;
      fAPCLAWS2: flow path FGS1 -> PortGroupConnection10 -> THR_AP.fAPCLAWS2
         -> PortGroupConnection6 -> servo2;
  end prAutoPilot.impl;
  
  thread thrAutoPilot
    features
      pfd1In: feature group Integrator::FGS::Displays::CDU::PFD_OutDataSkt;
      pfd1Out: feature group Integrator::FGS::Displays::CDU::PFD_InDataSkt;
      pfd2In: feature group Integrator::FGS::Displays::CDU::PFD_OutDataSkt;
      pfd2Out: feature group Integrator::FGS::Displays::CDU::PFD_InDataSkt;
      servo1: out feature  Integrator::FGS::SCS::SCS_Data;
      servo2: out feature  Integrator::FGS::SCS::SCS_Data;
      otherAP_A: feature group AP_CrossPlg;
      otherAP_B: feature group AP_CrossSkt;
      FGS2: feature group AP_DataSkt;
      FGS1: feature group AP_DataSkt;
    flows
      fAPCLAWS1: flow path FGS1 -> servo1 {
        Latency => 3 ms .. 3 ms;
        };
      fAPCLAWS2: flow path FGS1 -> servo2 {
        Latency => 3 ms .. 3 ms;
        };
    properties
      SEI::MIPSBudget => 50.0 MIPS;
      SEI::RAMBudget => 1.6 MByte;
      SEI::ROMBudget => 16.0 MByte;
      Dispatch_Protocol => Periodic;
      Deadline => 3 ms;
      Period => 5 ms;
  end thrAutoPilot;
  
  thread implementation thrAutoPilot.impl
    flows
      fAPCLAWS1: flow path FGS1 -> servo1;
      fAPCLAWS2: flow path FGS1 -> servo2;
  end thrAutoPilot.impl;
  
  feature group AP_DataPlg
    features
      dmy: out event data port;
  end AP_DataPlg;
  
  feature group AP_DataSkt
    features
      dmy: in event data port;
    inverse of AP_DataPlg
  end AP_DataSkt;
  
  feature group AP_CrossPlg
    features
      dmy: out event data port;
  end AP_CrossPlg;
  
  feature group AP_CrossSkt
    features
      dmy: in event data port;
    inverse of AP_CrossPlg
  end AP_CrossSkt;
end Integrator::FGS::AP;
